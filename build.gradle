apply from: 'repository.gradle'

allprojects {
	group = 'com.github.diplodoc'
	version = '0.0.6-SNAPSHOT'

    apply plugin: 'jacoco'

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
	apply plugin: 'groovy'

	repositories {
	    jcenter()
	}

	dependencies {
		compile dependency.groovy
		compile dependency.spring.context

        testCompile dependency.spock
        testCompile dependency.cglib
        testCompile dependency.objenesis
	}

    test {
        test.reports.html.enabled = true
    }
}

task testFailReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/results")
    reportOn subprojects*.test*.binResultsDir
}

task testResultsReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/results")
    reportOn subprojects*.test
}

task testCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each { sourceSets it.sourceSets.main }

    reports {
        xml.enabled false
        html.enabled true
        html.destination "${buildDir}/reports/coverage"
        csv.enabled false
    }

    dependsOn {
        subprojects*.test
    }
}

task clean(type: Delete) {
    delete "$buildDir/reports"
}

task testReport(type: DefaultTask) {
    dependsOn {
        [ testResultsReport, testCoverageReport ]
    }
}

project(':diplobase:diplobase-shell') {
	dependencies {
		compile project(':diplobase')
	}
}
project(':diplocore') {
	dependencies {
		compile project(':diplobase')
	}
}

project(':diploexec') {
	dependencies {
		compile project(':diplocore')
		compile project(':diplobase')
	}
}

project(':diploexec:diploexec-shell') {
	dependencies {
        compile project(':diplobase')
		compile project(':diploexec:diploexec-client')
	}
}

project(':diploexec:diploexec-client') {
	dependencies {
		compile project(':diplobase')
	}
}